import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  kotlin("jvm")
  id("java-gradle-plugin")
}

repositories {
  google()
  mavenCentral()
}

dependencies {
  implementation(project(":expo-autolinking-plugin-shared"))
  implementation(gradleApi())
  compileOnly("com.android.tools.build:gradle:8.5.0")

  testImplementation("junit:junit:4.13.2")
  testImplementation("com.google.truth:truth:1.1.2")
  testImplementation("io.mockk:mockk:1.14.2")
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
    vendor.set(JvmVendorSpec.ADOPTIUM) // opcional
  }
}


tasks.withType<KotlinCompile> {
  kotlinOptions {
    jvmTarget = "17"
  }
}

group = "expo.modules"

gradlePlugin {
  plugins {
    create("expoAutolinkingSettingsPlugin") {
      id = "expo-autolinking-settings"
      implementationClass = "expo.modules.plugin.ExpoAutolinkingSettingsPlugin"
    }
  }
}

tasks.withType<Test>().configureEach {
  testLogging {
    exceptionFormat = TestExceptionFormat.FULL
    showExceptions = true
    showCauses = true
    showStackTraces = true
  }
}

